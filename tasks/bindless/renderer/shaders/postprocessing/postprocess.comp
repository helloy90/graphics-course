#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : require

#include "tonemapping_utils.glsl"

layout(local_size_x = 32, local_size_y = 32) in;

layout(binding = 0, r11f_g11f_b10f) restrict uniform image2D renderImage;

layout(binding = 1) readonly buffer distributionBuffer {
    float bins[]; 
};

layout(binding = 2) readonly buffer histogramInfo_t {
    float binStepSize; // absolute
    float minWorldLuminance;    // relative
    float maxWorldLuminance;    // relative
    float minWorldBrightness;
    float maxWorldBrightness;
};

layout(push_constant) uniform params {
    uint len; 
};

void main() {
    ivec2 texCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 renderImageSize = imageSize(renderImage);
    if (texCoord.x >= renderImageSize.x || texCoord.y >= renderImageSize.y) {
        return;
    }
    vec3 color = imageLoad(renderImage, texCoord).rgb;

    float luminance = getAbsoluteLuminance(color, minWorldLuminance, maxWorldLuminance);
    
    uint currentBin = getBin(luminance, binStepSize, len);
    float adjustedLuminance = 
        minDisplayLuminance 
        + (maxDisplayLuminance - minDisplayLuminance)
        * bins[currentBin];

    color = color * adjustedLuminance / luminance;
    imageStore(renderImage, texCoord, vec4(color, 1.0));
}