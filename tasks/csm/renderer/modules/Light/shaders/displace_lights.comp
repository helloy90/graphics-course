#version 460
#extension GL_GOOGLE_include_directive : require

#include "../Light.h"
#include "LightParams.h"

layout(local_size_x = 128) in;

layout(binding = 0) readonly uniform params_t {
  LightParams params;
};

layout (binding = 1) readonly uniform height_params_t {
  float heightAmplifier;
  float heightOffset;
};

layout(binding = 2, r32f) readonly uniform image2D terrainMap;

layout(binding = 3) buffer lights {
    Light lightsBuffer[];
};

void main() {
    uint index = uint(gl_GlobalInvocationID.x);
    if (index >= params.lightsAmount) {
        return;
    }

    Light currentLight = lightsBuffer[index];

    ivec2 lightPosOnTerrainMap = ivec2(currentLight.pos.xz);

    float height = (imageLoad(terrainMap, lightPosOnTerrainMap).x - heightOffset) * heightAmplifier;

    currentLight.worldPos = vec4(currentLight.pos, 1);
    currentLight.worldPos.y += height;

    lightsBuffer[index] = currentLight;
}
